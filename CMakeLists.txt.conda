cmake_minimum_required(VERSION 3.1)
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED True)

set(CMAKE_MACOSX_RPATH 1)
set(CMAKE_CXX_FLAGS_RELEASE "-O2 -fopenmp")
set(CMAKE_CXX_FLAGS_DEBUG "-g -Wall -O0 -D_DEBUG")
#set(CMAKE_BUILD_TYPE DEBUG)
set(CMAKE_BUILD_TYPE RELEASE)

project(alm)

if(WIN32)
  LINK_DIRECTORIES(C:\\lapack)
  LINK_DIRECTORIES(C:\\spglib)
  include_directories(C:\\spglib)
  set(LAPACK_LIBRARIES lapack)
  set(spglib symspg)
endif()

if (UNIX)
  include_directories($ENV{CONDA_PREFIX}/include)
  link_directories($ENV{CONDA_PREFIX}/lib)
  #set(ENV{BLA_VENDOR} "Intel10_64lp")
  #find_package(LAPACK REQUIRED)
  set(LAPACK_LIBRARIES "-llapack")
  #set(BLAS_LIBRARIES "-lopenblas")
  set(spglib "-lsymspg")
endif()

#
# Add openmp flag if available
#
find_package(OpenMP)
if (OPENMP_FOUND)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
  set(openmp "-lgomp")
else()
  set(openmp "-lomp")
endif()

find_package(Boost REQUIRED)
include_directories(${Boost_INCLUDE_DIRS})
find_package(Eigen3 REQUIRED)
include_directories(${EIGEN3_INCLUDE_DIR})

if (WITH_HDF5_SUPPORT)
    add_definitions(-D_HDF5)
    find_package(HDF5 REQUIRED COMPONENTS CXX)
    include_directories(${HDF5_INCLUDE_DIRS})
    set(hdf5library ${HDF5_LIBRARIES})
endif()

# Version numbers
file(READ ${PROJECT_SOURCE_DIR}/src/version.h version_file)
string(REGEX MATCH "ALAMODE_VERSION = \"([0-9]+\\.[0-9]+\\.[0-9]+)\"" alm_version ${version_file})
set(alm_version ${CMAKE_MATCH_1})
MESSAGE("ALM version: ${CMAKE_MATCH_1}")
set(serial "${alm_version}")
set(soserial "1")

# Source code
include_directories("${PROJECT_SOURCE_DIR}/src")
set(SOURCES ${PROJECT_SOURCE_DIR}/src/alm.cpp
            ${PROJECT_SOURCE_DIR}/src/cluster.cpp
            ${PROJECT_SOURCE_DIR}/src/constraint.cpp
            ${PROJECT_SOURCE_DIR}/src/fcs.cpp
            ${PROJECT_SOURCE_DIR}/src/files.cpp
            ${PROJECT_SOURCE_DIR}/src/input_parser.cpp
            ${PROJECT_SOURCE_DIR}/src/input_setter.cpp
            ${PROJECT_SOURCE_DIR}/src/optimize.cpp
            ${PROJECT_SOURCE_DIR}/src/patterndisp.cpp
            ${PROJECT_SOURCE_DIR}/src/rref.cpp
            ${PROJECT_SOURCE_DIR}/src/symmetry.cpp
            ${PROJECT_SOURCE_DIR}/src/system.cpp
            ${PROJECT_SOURCE_DIR}/src/timer.cpp
            ${PROJECT_SOURCE_DIR}/src/writer.cpp)

set(HEADERS ${PROJECT_SOURCE_DIR}/src/alm.h
            ${PROJECT_SOURCE_DIR}/src/cluster.h
            ${PROJECT_SOURCE_DIR}/src/constraint.h
            ${PROJECT_SOURCE_DIR}/src/fcs.h
            ${PROJECT_SOURCE_DIR}/src/files.h
            ${PROJECT_SOURCE_DIR}/src/input_parser.h
            ${PROJECT_SOURCE_DIR}/src/input_setter.h
            ${PROJECT_SOURCE_DIR}/src/optimize.h
            ${PROJECT_SOURCE_DIR}/src/patterndisp.h
            ${PROJECT_SOURCE_DIR}/src/rref.h
            ${PROJECT_SOURCE_DIR}/src/symmetry.h
            ${PROJECT_SOURCE_DIR}/src/system.h
            ${PROJECT_SOURCE_DIR}/src/timer.h
            ${PROJECT_SOURCE_DIR}/src/writer.h)

# Executable
add_executable(alm ${PROJECT_SOURCE_DIR}/src/main.cpp
                   ${PROJECT_SOURCE_DIR}/src/alm_cui.cpp
                   ${SOURCES} ${HEADERS})
target_link_libraries(alm ${Boost_LIBRARIES} ${BLAS_LIBRARIES} ${LAPACK_LIBRARIES} ${spglib} ${openmp})
if (WITH_HDF5_SUPPORT)
    target_link_libraries(alm ${hdf5library})
endif()
install(TARGETS alm DESTINATION ${PROJECT_SOURCE_DIR}/bin)

# Shared library
add_library(almcxx SHARED ${SOURCES})
target_link_libraries(almcxx ${Boost_LIBRARIES} ${BLAS_LIBRARIES} ${LAPACK_LIBRARIES} ${spglib} ${openmp})
if (WITH_HDF5_SUPPORT)
    target_link_libraries(almcxx ${hdf5library})
endif()
set_property(TARGET almcxx PROPERTY VERSION ${serial})
set_property(TARGET almcxx PROPERTY SOVERSION ${soserial})
install(TARGETS almcxx DESTINATION ${PROJECT_SOURCE_DIR}/lib)

# Static link library
add_library(almcxx_static STATIC ${SOURCES})
set_property(TARGET almcxx_static PROPERTY VERSION ${serial})
set_property(TARGET almcxx_static PROPERTY SOVERSION ${soserial})
set_property(TARGET almcxx_static PROPERTY OUTPUT_NAME almcxx)
install(TARGETS almcxx_static ARCHIVE DESTINATION ${PROJECT_SOURCE_DIR}/lib)

# Header file
install(FILES ${PROJECT_SOURCE_DIR}/src/alm.h DESTINATION ${PROJECT_SOURCE_DIR}/include)
